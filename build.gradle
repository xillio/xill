/*
* Please do not change this file unless explicitly instructed to. The values you can change are found in gradle.properties
*/
plugins {
    id "org.sonarqube" version "1.0"
    id "com.google.osdetector" version "1.3.1"
}

apply plugin: 'jacoco'

//Test vpn connection
if (testConnection(mavenRepositoryHost)) {
    logger.info "We have a connection to $mavenFriendlyServerName"

    addRepositories(mavenRepositoryHost)
} else if (testConnection(mavenRepositoryHostFallback)) {
    logger.info "We have a connection to $mavenFriendlyServerName through an ssh tunnel"
    addRepositories(mavenRepositoryHostFallback)
} else {
    logger.error "Unable to connect to $mavenFriendlyServerName\n" +
            "Connect to office VPN or read the wiki to see how to connect to $mavenFriendlyServerName\n" +
            "link: $mavenRepositoryConnectionGuide"
}

allprojects {
    apply plugin: 'jacoco'

    /**
     * JAVA
     */
    apply plugin: 'eclipse'
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'nl.xillio'
    version = version

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    build.dependsOn javadoc
    javadoc.failOnError = !Boolean.parseBoolean(javadocIgnoreErrors)

    /**
     * MAVEN PUBLISH
     */
    if (project.name != 'xill-plugins') {
        apply plugin: 'maven-publish'

        publishing {
            publications {
                main(MavenPublication) {
                    artifactId 'xill-' + project.name

                    from components.java

                    artifact sourceJar {
                        classifier "sources"
                    }
                }
            }

            repositories {
                maven {
                    url 'http://$mavenRepositoryHost:$mavenRespoitoryPort/nexus/content/repositories/releases'
                    name 'release'
                    credentials {
                        username = mavenUsername
                        password = mavenPassword
                    }
                }
            }
        }

    }

    /**
     * TESTING
     */
    test.useTestNG {
        useDefaultListeners = true
    }


    dependencies {
        testCompile 'org.testng:testng:6.9.4'
        testCompile "org.mockito:mockito-core:1.+"
    }

    jar.manifest {
        attributes "Implementation-Version": version
        attributes "Build-Date": new Date().format("dd-MM-yyyy")
        attributes "Build-Java-Version": System.properties["java.version"]

        if (project.hasProperty(project.name + "ProjectName")) {
            attributes "Implementation-Title": project.getProperties().get(project.name + "ProjectName")
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

}

/**
 * APPLICATION
 */
evaluationDependsOnChildren()
description = 'this root project contains the overview of the contenttools application'
def mainClassName = "nl.xillio.contenttools.Application"
def pluginsDir = new File(buildDir, '../plugins')
def esConsoleDir = new File(buildDir, '../ESConsole')
def helpFilesDir = new File(buildDir, '../helpfiles')

dependencies {
    compile project(':api')
}

/**
 * PLUGINS
 */
task assemblePlugins {
    description 'Assemble and collect all plugins'
    group 'build'
    outputs.dir pluginsDir

    // Iterate over all plugins
    project(':xill-plugins').subprojects {
        def plugin = it
        assemblePlugins.inputs.dir plugin.libsDir
        assemblePlugins.dependsOn plugin.assemble
        doLast {
            copy {
                from plugin.libsDir
                // We move every plugin to its own folder
                into new File(pluginsDir, plugin.name)
            }
        }
    }
}
task assembleContenttools {
    description 'Assamble and collect the IDE, Processor and plugins'
    group 'build'
    def ide = project(':ide')
    def processor = project(':processor')

    dependsOn ide.assemble
    dependsOn processor.assemble
    dependsOn assemblePlugins

    inputs.dir pluginsDir
    inputs.dir ide.libsDir
    inputs.dir processor.libsDir

    doLast {
        copy {
            from ide.configurations.runtime.files
            from ide.libsDir
            from processor.configurations.runtime.files
            from processor.libsDir
            into pluginsDir
        }
    }
}
build.dependsOn assembleContenttools

/**
 * DEPLOYMENT
 */
apply from: 'http://dl.bintray.com/content/shemnon/javafx-gradle/8.1.1/javafx.plugin'

assemble.dependsOn.remove(jfxDeploy)

jfxDeploy {
    dependsOn assembleContenttools
    doFirst {
        copy {
            from pluginsDir
            into new File(libsDir, "plugins")
        }
    }
    inputFiles = files(
            libsDir,
            new File(projectDir, "CHANGELOG.md"),
            new File(projectDir, "LICENSE"))

    doLast {
        distsDir.eachDir() {
            dir -> deployZip.from dir
        }
    }
}

task deployZip(type: Zip, dependsOn: 'jfxDeploy') {
    group 'build'
    description "Deploy the application into a zip archive"

    // Set the base name
    baseName appID

    // Set the classifier
    def osName = osdetector.classifier
    if("linux" == osdetector.os) {
        osName += "-" + getLinuxReleaseSuffix()
    }

    classifier osName
}

task deploy(dependsOn: "deployZip") {
    group 'build'
    description "Deploy the application"
}

/**
 * JavaFX Configuration
 * Note that INNO_HOME env variable needs to be set to the INNO SETUP installation directory
 */
javafx {
    appID = appID
    appName = friendlyName
    copyright = copyrightInformation
    description = description
    licenseType = licenseType
    mainClass mainClassName
    vendor = company
    menu = true
    shortcut = true
    embedLauncher = false
    embedJNLP = false
    // Allow the user to set the heap size
    bundleArguments.userJvmOptions = ['-Xmx': '256m', '-Xms': '256m']
}

/**
 * CLEAN
 */
task cleanRuntime(type: Delete) {
    description 'Deletes all the folders generated and used by contenttools running in eclipse'

    delete pluginsDir, esConsoleDir, helpFilesDir
}

clean.dependsOn cleanRuntime

/**
 * VPN/SSH CONNECTION
 */

def testConnection(host) {
    try {
        def socket = new Socket();
        socket.connect(new InetSocketAddress(host, Integer.parseInt(mavenRespoitoryPort)), Integer.parseInt(mavenTimeout))
        socket.close()

        return true;
    } catch (IOException ex) {
        return false;
    }
}

def addRepositories(host) {
    allprojects {
        repositories {
            maven {
                url "http://$host:$mavenRespoitoryPort/nexus/content/repositories/releases"
                credentials {
                    username mavenUsername
                    password mavenPassword
                }
            }
            maven {
                url "http://$host:$mavenRespoitoryPort/nexus/content/repositories/thirdparty"
                credentials {
                    username mavenUsername
                    password mavenPassword
                }
            }
        }
    }
}

def getLinuxReleaseSuffix() {
    if (osdetector.release.isLike('debian')) {
        return 'debian'
    } else if (osdetector.release.isLike('redhat')) {
        return 'redhat'
    } else {
        return 'other'
    }
}

run.dependsOn(assemblePlugins)