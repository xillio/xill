/**
 * This file represents the base for every project
 */
ext {
    pluginsDir = rootProject.file('plugins')
}
apply plugin: 'java'
apply plugin: 'jacoco'

task sourceJar(type: Jar) {
    group 'build'
    description 'Build a jar containing java sources'

    from sourceSets.main.allJava
}

version rootProject.version
group mavenGroup

apply from: rootProject.file('gradle/publish.gradle')
apply from: rootProject.file('gradle/deploy.gradle')
apply from: rootProject.file('gradle/license.gradle')

tasks.withType(Jar) {
    doFirst {
        manifest {
            attributes "Implementation-Version": version
            attributes "Build-Date": new Date().format("dd-MM-yyyy")
            attributes "Build-Java-Version": System.properties["java.version"]
            if (project.hasProperty(project.name + "ProjectName")) {
                attributes "Implementation-Title": project.getProperties().get(project.name + "ProjectName")
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

test.useTestNG {
    useDefaultListeners = true
}


dependencies {
    testCompile 'org.testng:testng:6.9.4'
    testCompile "org.mockito:mockito-core:1.+"
}